// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript { scriptHandler ->
    apply from: 'repositories.gradle',
            to: scriptHandler

    ext.kotlin_version = "1.4.21"

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.8.2'
    id "com.smallraw.plugin" apply false
}

ext {
    // query git for the commit count to automate versioning.
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD master'.execute([], project.rootDir).text.trim())
}

subprojects {
    buildscript {
        apply from: rootProject.file('repositories.gradle')
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        java {
            // This path needs to be relative to each project
            target fileTree('.') {
                include '**/src/*/java/**/*.java'
                exclude '**/.gradle/**'
                exclude '**/generated/**'
                exclude '**/build/install/**'
            }
            removeUnusedImports()
            googleJavaFormat("1.7").aosp()
            importOrder 'java', '', 'org.smallraw', '\\#'
            trimTrailingWhitespace()
            endWithNewline()
            licenseHeaderFile "$rootDir/gradle/spotless/java.license"
        }
        kotlin {
            // This path needs to be relative to each project
            target fileTree('.') {
                include '**/*.kt'
                exclude '**/.gradle/**'
                exclude '**/build/install/**'
            }
            ktlint(Versions.ktlint)
            trimTrailingWhitespace()
            endWithNewline()
            licenseHeaderFile "$rootDir/gradle/spotless/java.license"
        }
        groovyGradle {
            target '*.gradle'
            greclipse().configFile("$rootDir/gradle/spotless/formatter.properties")
            endWithNewline()
            indentWithSpaces(4)
        }
    }
}
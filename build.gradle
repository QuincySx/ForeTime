// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript { scriptHandler ->
    apply from: 'repositories.gradle',
            to: scriptHandler

    ext.versions = [
            'compileSdk'           : 28,
            'minSdk'               : 23,
            'targetSdk'            : 28,
            'androidx'             : '1.0.0',
            'androidxCollection'   : '1.1.0',
            'androidxCoreRuntime'  : '2.1.0-rc01',
            'androidxArch'         : '2.1.0-rc01',
            'coreKtx'              : '1.2.0-alpha02',
            'androidxLifecycle'    : '2.2.0-alpha02',
            'appcompat'            : '1.1.0-rc01',
            'recyclerview'         : '1.0.0',
            'cardview'             : '1.0.0',
            'constraintLayout'     : '1.1.3',
            'material'             : '1.1.0-alpha08',
            'dagger'               : '2.23.2',
            'extJunit'             : '1.1.0',
            'glide'                : '4.9.0',
            'glideTransformations' : '4.0.1',
            'gson'                 : '2.8.5',
            'kotlin'               : '1.3.41',
            'kotlinxCoroutinesCore': '1.3.0-M2',
            'ktlint'               : '0.24.0',
            'okhttp'               : '3.10.0',
            'retrofit'             : '2.6.0',
            'eventbus'             : "3.1.1",
            'room'                 : '2.2.0-alpha01',
            'autoSize'             : '1.1.2',
            'statusBarCompat'      : '2.3.3',
            'supportLibrary'       : '28.0.0',
            'test_junit'           : '4.12',
            'test_espresso'        : '3.2.0',
            'test_runner'          : '1.2.0'
    ]
    ext.names = [
            'applicationId': 'com.smallraw.foretime.app',
            'versionName'  : '1.0.0'
    ]

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.23.1'
}

ext {
    // query git for the commit count to automate versioning.
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD master'.execute([], project.rootDir).text.trim())
}

subprojects {
    buildscript {
        apply from: rootProject.file('repositories.gradle')
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
        }
    }
}